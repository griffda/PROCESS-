#   PROCESS f90wrap run
#   Author    :   K. Zarebski (UKAEA)
#   Date      :   last modified 2020-11-5
#
#   Run f90wrap on the given files list

MACRO(F90WRAP)
    SET(F90WRAP_NAME "f90wrap")
    SET(FORTRAN_INTERFACE_NAME "fortran")
    SET(F90WRAP_PYTHON_OUT ${CMAKE_BINARY_DIR}/${FORTRAN_INTERFACE_NAME}.py)
    SET(F90WRAP_PYTHON_MODULE ${PYTHON_MODULE_DIR}/${FORTRAN_INTERFACE_NAME}.py)
    SET(F90WRAP_KIND_FILE ${PROCESS_SRC_DIR}/kind_map)
    SET(F90WRAP_IMPORT_CONVERTER ${CMAKE_SOURCE_DIR}/scripts/f90wrap_interface_import_modifier.py)
    LIST(TRANSFORM PROCESS_SRCS PREPEND ${F90WRAP_NAME}_ OUTPUT_VARIABLE F90WRAP_OUTPUT_FILES)
    LIST(TRANSFORM PROCESS_SRCS PREPEND ${PROCESS_SRC_DIR}/ OUTPUT_VARIABLE F90WRAP_INPUT_FILES)
    MESSAGE(STATUS "F90WRAP_INPUTS: ${FORTRAN_INTERFACE_NAME}")
    LIST(TRANSFORM F90WRAP_OUTPUT_FILES PREPEND ${CMAKE_BINARY_DIR}/ OUTPUT_VARIABLE F90WRAP_OUTPUT_FILES)

    MESSAGE(STATUS "[f90Wrap]: ")
    MESSAGE(STATUS "\tKind Map File: ${F90WRAP_KIND_FILE}")
    MESSAGE(STATUS "\tOutput File: ${F90WRAP_PYTHON_OUT}")
    ADD_CUSTOM_TARGET(
        ${F90WRAP_NAME}
        DEPENDS ${F90WRAP_OUTPUT_FILES} ${F90WRAP_PYTHON_OUT} ${F90WRAP_PYTHON_MODULE}
    )

    ADD_CUSTOM_COMMAND(
        OUTPUT ${F90WRAP_OUTPUT_FILES} ${F90WRAP_PYTHON_OUT} ${F90WRAP_PYTHON_MODULE}
        COMMAND ${F90WRAP_NAME} -k ${F90WRAP_KIND_FILE} ${F90WRAP_INPUT_FILES} -m ${FORTRAN_INTERFACE_NAME}
        COMMAND ${PYTHON_EXECUTABLE} ${F90WRAP_IMPORT_CONVERTER} ${F90WRAP_PYTHON_OUT}
        COMMAND ${CMAKE_COMMAND} -E copy ${F90WRAP_PYTHON_OUT} ${F90WRAP_PYTHON_MODULE}
    )

    ADD_DEPENDENCIES(${F90WRAP_NAME} ${PIP_NAME} ${PROJECT_NAME})

ENDMACRO(F90WRAP)